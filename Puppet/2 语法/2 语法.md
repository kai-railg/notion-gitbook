# 2 语法

- 请先阅读 `[Puppet的语言风格与语法格式](Puppet的语言风格与语法格式)`
- 变量
    - 几乎所有的东西和符号在puppet里面都被看作是字符串
    - 变量名称区分大小写，并且必须以美元符号 `$` 开头
        - 如果第一个字符是下划线，则只能从其自己的本地范围访问该变量
    - 默认情况下，未分配的变量的值为`undef`
    - 变量`$title`和 `$name`都自动设置为类名，因此您不能将它们用作参数
    - puppet 不允许你在同一个作用域里面对一个变量进行两次赋值.

    ```python
    $content = "some content\n"
    ```

    - 要从数组分配多个变量，必须指定相等数量的变量和值

    ```python
    [$a, $b, $c] = [1,2,3]      # $a = 1, $b = 2, $c = 3
    [$a, [$b, $c]] = [1,[2,3]]  # $a = 1, $b = 2, $c = 3
    [$a, $b] = [1, [2]]            # $a = 1, $b = [2]
    [$a, [$b]] = [1, [2]]          # $a = 1, $b = 2  
    ```

    - 在哈希中包含额外的键/值对，但是运算符左侧的所有变量必须在哈希中具有对应的键

    ```python
    [$a, $c] = {a => 5, b => 10, c => 15, d => 22}  # $a = 5, $c = 15
    ```

    - 可以在任何接受变量数据类型值的地方使用变量名，包括表达式，函数和资源属性

    ```python
    file {'/tmp/testing':
      ensure  => file,
      content => $content,
    }

    $address_array = [$address1, $address2, $address3]
    ```

    - Puppet可以解析双引号字符串中包含的变量，这称为插值。 $用大括号将变量名（之后的部分 ）括起来,该语法是可选的。这些可选的花括号仅允许在字符串内部使用

    ```python
    $rule = "Allow * from $ipaddress"
    file { "${homedir}/.vim":
      ensure => directory,
      ...
    }
    ```

    - 给定范围中的变量仅在该范围及其子范围内可用，并且任何局部范围都可以在本地覆盖它从其父级接收的变量

    ```bash
    $vhostdir = $apache::params::vhostdir   

    # 作用域::变量名，第一个名称空间的名称可以为空，表示顶级名称空间，比如 $::vhostdir
    # 也可以使用正则表达式
    \A\$([a-z][a-z0-9_]*)?(::[a-z][a-z0-9_]*)*::[a-z0-9_][a-zA-Z0-9_]*\Z
    ```

    此`$vhostdir`变量的限定名称表明已找到该变量并在`apache::params`该类中为其分配了一个值。可以在任何类，类型或节点定义之外分配变量。这些顶级范围变量的首个命名空间段具有一个空字符串，因此顶级范围变量的限定名称以双冒号开头，例如`$::osfamily`。

    - 变量的作用域
        - 本地 → 父级 → 顶级

        ```python
        # /etc/puppetlabs/code/modules/scope_example/manifests/init.pp
        class scope_example {
          $variable = "Hi, I'm local!"
          notify {"Message from here: $variable":}
        }

        # /etc/puppetlabs/code/environments/production/manifests/site.pp
        $variable = "Hi, I'm top!"

        node 'puppet.example.com' {
          $variable = "Hi, I'm node!"
          include scope_example
        }
        ```

        执行：

        ```python
        $ puppet apply site.pp
        notice: Message from here: Hi, I'm local!
        ```

- 资源
    - 至少每个资源声明都有一个资源类型，一个标题和一组属性
    - 属性描述资源的期望状态，每种资源类型都有其自己的一组可用属性
    - 目标系统上的每个资源都必须具有唯一标识。例如，您不能拥有两个具有相同名称的服务。Puppet中的此标识属性称为`namevar`。
    - 每种资源类型都有一个指定为用作`namevar`的属性。对于大多数资源类型，这是 name属性，但是某些类型使用其他属性，例如`file`类型，该类型path将文件在磁盘上的位置用作`namevar`。

    - 声明示例

        ```bash
        <TYPE> { '<TITLE>': <ATTRIBUTE> => <VALUE>, }
        ```

        ```bash
        file { "/etc/hosts":   # file: 资源的类型, "tec/hosts":资源的title,冒号前面的名字          
            ensure => file,    # ensure属性可确保创建指定的文件（如果节点上尚不存在）
        		owner => root,     # 资源的用户,都是资源的属性， = 或是 => 
            group => root,     # 资源的用户组
            mode => 644        # 资源的权限
        }
        ```

    - 声明默认资源属性
        - 可以一次设置许多属性，但是您仍然可以覆盖其中的一些属性。

        ```bash
        file {
          default:
            ensure => file,
            owner  => "root",
            group  => "wheel",
            mode   => "0600",
          ;
          ['ssh_host_dsa_key', 'ssh_host_key', 'ssh_host_rsa_key']:
            # use all defaults
          ;
          ['ssh_config', 'ssh_host_dsa_key.pub', 'ssh_host_key.pub', 'ssh_host_rsa_key.pub', 'sshd_config']:
            # override mode
            mode => "0644",
          ;
        }

        # 更便捷的语法， *，一个资源中仅可使用一次
        $file_ownership = {
          "owner" => "root",
          "group" => "wheel",
          "mode"  => "0644",
        }

        file { "/etc/passwd":
          ensure => file,
          *      => $file_ownership,
        }
        ```

    - 抽象资源类型
        - 以下三个示例等效于彼此

        ```bash
        # 1
        file { "/tmp/foo": ensure => file, } File { "/tmp/foo": ensure => file, } Resource[File] { "/tmp/foo": ensure => file, }

        # 2
        $mytype = File
        Resource[$mytype] { "/tmp/foo": ensure => file, }

        # 3
        $mytypename = "file"
        Resource[$mytypename] { "/tmp/foo": ensure => file, }
        ```

    - 标题数组资源
        - Puppet会创建具有相同属性集的多个资源，相当于遍历并创建

        ```bash
        $rc_dirs = [
          '/etc/rc.d',       '/etc/rc.d/init.d','/etc/rc.d/rc0.d',
          '/etc/rc.d/rc1.d', '/etc/rc.d/rc2.d', '/etc/rc.d/rc3.d',
          '/etc/rc.d/rc4.d', '/etc/rc.d/rc5.d', '/etc/rc.d/rc6.d',
        ]

        file { $rc_dirs:
          ensure => directory,
          owner  => 'root',
          group  => 'root',
          mode   => '0755',
        }
        ```

    - 添加或修改属性
        - 使用资源引用，不能覆盖已经指定的属性

            ```bash
            file {'/etc/passwd':
              ensure => file,
            }

            File['/etc/passwd'] {
              owner => 'root',
              group => 'root',
              mode  => '0640',
            }
            ```

        - 使用收集器修改属性，覆盖任何已有的属性

            ```bash
            class base::linux { 
              file {'/etc/passwd':
                ensure => file,
              } 
              ...}

            include base::linux

            File <| tag == 'base::linux' |> {
             owner => 'root',
             group => 'root',
             mode => '0640',
            }
            ```

    - 本地默认资源
        - 是一种保护您的类和定义的类型免于意外继承经典资源默认值语句的方法。

        ```bash
        class mymodule::params {
          $file_defaults = {
            mode  => "0644",
            owner => "root",
            group => "root",
          }
          # ...
        }

        class mymodule inherits mymodule::params {
          file { default: *=> $mymodule::params::file_defaults;
            "/etc/myconfig":
              ensure => file,
            ;
          }
        }
        ```

- 资源类型参考
    - 定义资源的参数，比如 `file` 类型中的`path`
    - `[参数文档链接](https://puppet.com/docs/puppet/latest/type.html)`
    - file

        > 管理文件，包括其内容、所有权和权限。文件类型可以管理普通文件、目录和符号链接; 类型应该在 ensure 属性中指定。文件内容可以直接使用 content 属性管理，或者使用 source 属性从远程源下载

        - path，    要管理的文件的路径，默认值为资源的title
        - ensure，  文件类型，可选的值：`present, absent, file, directory, link`
            - present， 接受任何形式的文件存在，并创建一个正常的文件
            - absent，   确保文件不存在，并在必要时删除
            - file，         确保是一个正常的文件
            - directory， 确保是一个正常的目录，puppet不会破坏目录，除非指定`force`
            - link，        确保是一个软连接，你指定另一个文件的路径作为确保值，这等同于指定链接并使用该路径作为目标，比如`ensure => '/etc/inet/inetd.conf'`
        - content，   文件所需内容，此属性与`source` 和 `target` 互斥
        - group，      文件的所属组，参数可以是组名或组ID
        - owner，      文件所属用户，参数可以是用户名或用户ID
        - mode，       文件的权限，默认`“0644”`
        - source，     一个源文件，它将被复制到本地系统中的适当位置
            - puppet形式，`puppet:///modules/<MODULE NAME>/<FILE PATH>`
            - file形式，`/etc/puppetlabs/code/modules`
            - http形式，指定文件的链接
        - target，      创建链接的目标，和link结合使用。`source、links、content`三者互斥
        - links，         文件复制的方式，可选的参数有`follow`文件本身；`manage`软连接
        - backup，   文件内容是否(以及如何)在被替换之前进行备份
        - ctime，      只读状态的任意方式修改文件的最后时间
        - force，        强制的执行文件操作，可选的参数有 true、yes 或 flase、no
        - recurse，     递归的管理目录的内容
        - replace，     是否替换本地系统中已经存在但其内容与源或内容属性指定的内容不匹配的文件或符号链接
    - package
        - 管理软件包，会根据你所在的平台自动猜测你正在使用的打包格式
        - `Package[name]`不像许多其他资源类型那样是 `Package[title ]`的同义词
        - command，  管理包时的目标命令，相当于命令路径，默认default（程序定义）
        - ensure，  包的状态
            - installed：安装，默认的
            - latest：最新的
            - purged：分开管理配置的系统上，指定配置文件作为确保值来卸载配置文件
            - present： 确保创建
            - absent：   确保不存在
            - /./
        - source，  在哪里可以找到包文件
        - install_options， 安装包时要传递的附加选项数组，基于特定软件包，通常由软件商文档化，比如install_options => [ '/S', { 'INSTALLDIR' => 'C:\\mysql-5.5'}]
        - package_settings， 可以更改包的内容或配置的设置，基于特定软件包
        - reinstall_on_refresh， 是否通过重新安装包来相应刷新事件，默认false
        - responsefile，一个文件，其中包含对包提出的问题的任何必要的答案，指定的路径
        - 
    - service
        - 管理运行中服务
        - path，   init 脚本的路径
        - enable，   是否应该启动时启动服务，可选 `true、false、manual、mask`
        - ensure，  服务状态，可选的有 `stopped、running、false、true`
        - binary，   守护进程的路径，仅在init脚本不可用的平台上
        - start，     手动指定启动命令
        - 
    - user
        - 管理用户，主要针对系统用户
        - ensure，  用户状态， 可选参数 `present、absent、role`
        - comment，  用户的描述，通常是用户的全名
        - auths，     用户拥有的验证码，多个的话为数组
        - expiry，    用户的过期时间，可选参数：absent、`/^\d{4}-\d{2}-\d{2}$/`
        - gid，         用户的主要组
        - groups，   用户所属的组，不用列出主组，多个为数组
        - home，     用户的家目录
        - password，用户的密码，要求加密
        - profiles，   用户拥有的配置文件
        - role，         用户的角色，需要管理 solaris rbac 的特性
    - group
        - 管理用户组。
        - ensure，  创建或删除组，可选参数有：`present、absent`
        - 
    - exec
        - command，实际执行的命令
        - creates，    运行之前要查找的文件，如果存在命令则不会运行
        - cwd，          运行命令的目录，必须存在
        - user，          运行命令的用户
        - group，       运行命令的组
        - path，          用于命令执行的搜索路径，如果没有指定路径，则命令必须完全限定
        - environment，为命令提供的变量数组，比如`[ 'HOME=/root']`
        - provider，   指定平台，可选的有 `posix、shell、window`
        - timeout，    超时时间，默认300s
        - tries，         尝试的次数，默认1

        - refreshonly，在依赖对象发生更改时作为刷新机制运行，可选的有 `true、false`
            - 只有订阅和通知才能触发操作
        - logoutput，    记录命令输出日志
    - filebucket
        - 远程文件桶，用于备份，master服务器自动提供
        - path， 到本地 filebucket 的路径; 默认为 clientbuckedir 设置的值。 若要使用远程文件桶，必须将此属性设置为 false
        - port，  远程服务器端口，必须在 path 属性设置为 false 。
        - server，  提供远程文件桶服务的服务器，必须在 path 属性设置为 false 。
    - notify
        - 向代理运行时日志发送任意消息，通知类型资源不是幂等的
        - message，  要发送到日志的消息。
        - withpath，  是否宣誓完整的路径，可选 true 或 false

    - resources
        - 元数据，指对托管的数据的处理
    - schedule
        - 定义 Puppet 的时间表。 资源可以通过使用调度元表来限制到一个调度
        - 目前，调度只能用于阻止资源被应用
        - 默认情况下每30分钟应用一次资源，时间表的安排是每小时只应用2次
        - 
- class 类
    - Puppet有两种声明类的主要方法：包含类和资源类
    - 资源类示例

        ```bash
        class apache (String $version = 'latest') {
          package {'httpd':
            ensure => $version, # Using the version parameter from above
            before => File['/etc/httpd.conf'],
          }
          file {'/etc/httpd.conf':
            ensure  => file,
            owner   => 'httpd',
            content => template('apache/httpd.conf.erb'), # Template from a module
          }
          service {'httpd':
            ensure    => running,
            enable    => true,
            subscribe => File['/etc/httpd.conf'],
          }
        }
        ```

    - 包含类示例
        - 声明有四种方式：`include`，`require`，`contain`，和`hiera_include`
        - include

            ```bash
            include base::linux
            include Class['base::linux']
            include base::linux, apache

            $my_classes = ['base::linux', 'apache']
            include $my_classes

            ```

        - require
            - 该require函数声明一个或多个类，然后使它们成为周围容器的依赖项

            ```bash
            define apache::vhost (Integer $port, String $docroot, String $servername, String $vhost_name) {
              require apache
              ...
            }
            # Puppet确保apache在任何apache::vhost 实例中的任何资源之前应用类中的每个资源
            ```

        - contain
            - 该contain函数在另一个类定义中使用，以声明一个或多个类并将这些类包含在周围的类中。这将强制类的排序。当您在另一个类中包含一个类时，包含类的关系也将扩展到所包含的类

            ```bash
            class ntp {
              file { '/etc/ntp.conf':
                ...
                require => Package['ntp'],
                notify  => Class['ntp::service'],
              }
              contain ntp::service
              package { 'ntp':
                ...
              }
            }

            # ntp该类包含ntp::service该类。与ntp该类形成关系的任何资源也与该类具有相同的关系 ntp::service
            ```

        - hiera_include
            - 
    - define (封装）
        - puppet里面有一个非常有用的语法结构,叫做"definitions", 通过 definitions 可以把多个资源包装成一个资源,或者把一个资源包装成一个模型,便于使用.例如,在debian里面管理一个apache虚拟机非常简单,把一个虚拟主机的配置文件放到/etc/sites-available/里面,然后做一个符号链接到/etc/sites-enabled目录. 你可以为你每个虚拟主机复制同样的配置代码,但是如果你使用下面的代码就会更好和更简单.
        - 示例1

            ```python
            define virtual_host($docroot, $ip, $order = 500, $ensure = "enabled") {
               $file = "/etc/sites-available/$name.conf"

               # The template fills in the docroot, ip, and name.
               file { $file:
                   content => template("virtual_host.erb"),
                   notify  => Service[apache]
               }
               file { "/etc/sites-enabled/$order-$name.conf":
                   ensure => $ensure ? {
                       enabled  => $file,
                       disabled => absent
                   }
               }
            }
            ```

            - 然后,你就可以使用这个定义来管理一个apache虚拟主机,如下面代码所示

            ```python
            virtual_host { "reductivelabs.com":
                order   => 100,
                ip      => "192.168.0.100",
                docroot => "/var/www/reductivelabs.com/htdocs"
            }
            ```

            - 你可以在其他地方重用这个定义, 另一个定义的用法就是包装一组exec资源,让使用者更加清晰思路.例如下面的代码实现了一个svn库的创建的定义.

            ```python
            # Create a new subversion repository.
            define svnrepo($path) {
                exec { "create-svn-$name":
                    command => "/usr/bin/svnadmin create $path/$name",
                    creates => "$path/$name" # only run if this file does not exist
                }
            }
            ```

            - 然后,你可以在其他地方用下面的代码来创建一个svn库

            ```python
            svnrepo { puppet: path => "/var/lib/svn" }
            ```

        - 示例2（建议）
            - 定义 define

                ```bash
                # /etc/puppetlabs/puppet/modules/apache/manifests/vhost.pp
                define apache::vhost (
                  Integer $port,
                  String[1] $docroot,
                  String[1] $servername = $title,
                  String $vhost_name = '*',
                ) {
                  include apache # contains package['httpd'] and service['httpd']
                  include apache::params # contains common config settings

                  $vhost_dir = $apache::params::vhost_dir

                  # the template used below can access all of the parameters and variable from above.
                  file { "${vhost_dir}/${servername}.conf":
                    ensure  => file,
                    owner   => 'www',
                    group   => 'www',
                    mode    => '0644',
                    content => template('apache/vhost-default.conf.erb'),
                    require  => Package['httpd'],
                    notify    => Service['httpd'],
                  }
                }
                ```

            - 声明define的资源

                ```bash
                apache::vhost {'homepages':
                 port    => 8081,
                 docroot => '/var/www-testhost', 
                }

                # 具有默认值的参数被视为可选参数,没有默认值的参数是必需参数
                ```

- 条件语句
    - if/elif/else

        ```bash
        if $facts['is_virtual'] {
          warning('Tried to include class ntp on virtual machine; this node might be misclassified.')
        }
        elsif $facts['os']['family'] == 'Darwin' {
          warning('This NTP module does not yet work on our Mac laptops.')
        }
        else {
          include ntp
        }
        ```

    - unless （除非，执行条件为false）

        ```bash
        unless $facts['memory']['system']['totalbytes'] > 1073741824 {
          $maxclient = 500 # $maxclient除非系统内存超出指定的参数，否则此语句设置为500
        }
        ```

    - case

        ```bash
        case $facts['os']['name'] {  # 选取特定变量赋值
          'Solaris':                     { include role::solaris }
          'RedHat', 'CentOS':            { include role::redhat  }
          /^(Debian|Ubuntu)$/:           { include role::debian  }
          default:                       { include role::generic }
        }
        ```

    - 选择器语句（返回一个值）

        ```bash
        $rootgroup = $facts['os']['family'] ? {  # ？表示读取特定的值
          'Solaris'                     => 'wheel',
          /(Darwin|FreeBSD)/            => 'wheel',
          default                       => 'root',
        }

        file { '/etc/passwd':
          ensure => file,
          owner  => 'root',
          group  => $rootgroup,
        }
        ```

- 函数和变量
    - 内置函数
        - `[所有内置函数](https://puppet.com/docs/puppet/latest/function.html)`
        - 语句函数像其他函数一样返回值，但是它们始终返回的值`undef`
        - 目录声明（和类有关）
            - include   在目录中包括指定的类
            - require   在目录中包括指定的类，并将其添加为当前类或已定义资源的依赖项
            - contain   将指定的类包括在目录中，并将它们包含在当前类中
            - tag          将一个或多个指定标签添加到包含的类或定义的资源中
        - 记录语句
            - debug   调试级别
            - info        信息级别
            - notice    在通知级别记录消息
            - warning
            - err
        - 失败陈述
            - fail     记录错误消息并终止编译
    - 函数调用
        - 前缀和链式两种，区别是语法格式
        - 前缀方法调用
            - 在以下示例中，`template`，`include`，和`each`是全部功能。该`template`函数用于其返回值， `include`将一个类添加到目录，并 `each`使用不同的值多次运行代码块

            ```bash
            file {"/etc/ntp.conf":
              ensure  => file,
              content => template("ntp/ntp.conf.erb"), # function call; resolves to a string
            }

            include apache # function call; modifies catalog

            $binaries = [
              "facter",
              "hiera",
              "mco",
              "puppet",
              "puppetserver",
            ]

            # function call with lambda; runs block of code several times
            each($binaries) |$binary| {
              file {"/usr/bin/$binary":
                ensure => link,
                target => "/opt/puppetlabs/bin/$binary",
              }
            }
            链式
            ```

        - 链式方法调用

            ```bash
            file {"/etc/ntp.conf":
              ensure  => file,
              content => "ntp/ntp.conf.erb".template, # function call; resolves to a string
            }

            apache.include # function call; modifies catalog

            $binaries = [
              "facter",
              "hiera",
              "mco",
              "puppet",
              "puppetserver",
            ]

            # function call with lambda; runs block of code several times
            $binaries.each |$binary| {
              file {"/usr/bin/$binary":
                ensure => link,
                target => "/opt/puppetlabs/bin/$binary",
              }
            }
            ```

    - `[内置变量文档](https://puppet.com/docs/puppet/latest/lang_facts_builtin_variables.html)`
    - `[保留名称](https://puppet.com/docs/puppet/latest/lang_reserved.html#reserved-words-and-naming)`
    - 自定义函数
        - 格式定义

            ```bash
            function <MODULE NAME>::<NAME>(<PARAMETER LIST>) >> <RETURN TYPE> {
              ... body of function ...
              final expression, which is the returned value of the function
            }

            # 1
            function apache::bool2http(Variant[String, Boolean] $arg) >> String {
              case $arg {
                false, undef, /(?i:false)/ : { 'Off' }
                true, /(?i:true)/          : { 'On' }
                default                    : { "$arg" }
              }
            }
            地点
            ```

- node
    - 节点定义(node definition), 节点定义很象类定义,也支持继承特性. 当一个节点(puppet客户端)连接到puppet服务器端,puppet解析器会查找这个节点的node代码片断,然后利用这个代码片断来生成该客户端的配置代码. puppet里面主机名来标明一个主机,因此主机名在puppet里面相当重要. 如果puppet找不到匹配该主机名的node定义,就会用默认的节点定义来配置该主机. 在node里面使用主机名,需要用单引号把主机名括起来.
    - 节点可以定义`类声明，变量，资源声明，收集器，条件语句，链接关系和函数的任何混合`
    - 示例

        ```bash
        # 1
        node 'db1.example.com' {
          include common
          include mysql
        }

        # 2
        node 'www1.example.com', 'www2.example.com', 'www3.example.com' {
          include common
          include apache, squid
        }

        # 3
        node /^(one|two)\.example\.com$/ {
          include common
        }

        # 4
        node /^www(\d+)$/ {
          $wwwnumber = $1   # 正则匹配的第一个变量
        }

        ```

- 其他
    - `[运算符](https://puppet.com/docs/puppet/latest/lang_expressions.html#reference-1893)`
    - `[正则](https://puppet.com/docs/puppet/latest/lang_data_regexp.html#reference-2639)`
    - `[数据类型](https://puppet.com/docs/puppet/latest/lang_data.html)`
    - `[迭代和循环](https://puppet.com/docs/puppet/latest/lang_iteration.html)`

- Inheritance 继承
    - puppet 支持有限制的类的继承, 但是它只有一个唯一的作用就是: 子类里面的属性可以覆盖父类里面的属性
    - 在子类里面的资源类型的定义是用的大写的 `File`; 表示对这个资源重新定义,如果用小写的 `file`;就会引起前面说的资源的重复配置的情况.得到一个语法错误.

    ```python
    class base {
      file { "/my/file": content = template("base.erb") }
    }

    class sub inherits base {
      # override the content
      File["/my/file"] { content = template("other.erb") }
    }
    ```

- 复杂的案例（安装、启动、重启）

    ```python
    class ssh {
      package { ssh: ensure = installed }
      file { sshd_config:
        name = $operatingsystem ? {
          Darwin  = "/etc/sshd_config",
          Solaris = "/opt/csw/etc/ssh/sshd_config",
          default = "/etc/ssh/sshd_config"
        },
        source = "puppet://server.domain.com/files/ssh/sshd_config"
      }
      service { ssh:
        name = $operatingsystem ? {
          Solaris = openssh,
          default = ssh
        },
        ensure    = running,
        subscribe = [Package[ssh], File[sshd_config]]
      }
    }
    ```

    - 上面的代码安装好ssh包,并开启ssh服务,因为在不同的操作系统上面,ssh的配置文件名字不一样,所以还可以通过判断操作系统的类型来指定资源的title(标题). 那么操作系统的类型是怎么判断的呢?
    - Facter Variables facter 变量
        - 引入 `$operatingsystem` 这个变量, 在puppet分析代码的时候,会把从 `facter` 传送过来的对应的值赋值给这个变量. 你可以单独手工执行`facter`这个命令,这个命令会打印出它所收集到的关于主机的信息,例如ip地址等等. `facter`把收集到值发送个puppet服务器端,服务器端就可以根据不同的条件来对不同的机器生成不同的puppet配置文件. 最重要的一个就是服务器的主机名.
    - selectors 选择器
        - 另一个新东西就是 `? { … }` 这个语法, 通过对 ? 前面的变量与括号里面的选择项进行对比,然后把相应的值赋值给资源,例如上面的例子, `$operatingsystem` 如何是`solaris` ,那么ssh这个资源的 name 属性就是 "openssh" ;如果不匹配,就赋值成defualt的指, 匹配将区分大小写.
    - title vs name
        - 配置资源相互关系的时候, 是用资源的title.
        - name是指定这个资源的具体路径. 通常,name 会默认和title的值一样.
    - 资源之间的关系
        - 最后一行的 `File[sshd_config]` 语法指定了一个资源参考,注意这里是用的资源的title ,这样你就不用指定资源的全路径.
        - `subscribe = [Package[ssh], File[sshd_config]]` 这个语句表示,如果`package "ssh"` 或者 文件" `sshd_config`"有修改, `service ssh`需要重启.